#! /usr/bin/osascript

(*
â—¸ Veritrope.com
Export All Safari Tabs to a Text File
VERSION 1.1
March 5, 2013

// UPDATE NOTICES
** Follow @Veritrope on Twitter, Facebook, Google Plus, and ADN for Update
Notices! **

// SUPPORT VERITROPE!
If this AppleScript was useful to you, please take a second to show your love
here: http://veritrope.com/support

// SCRIPT INFORMATION AND UPDATE PAGE
http://veritrope.com/code/export-all-safari-tabs-to-a-text-file

// REQUIREMENTS
More details on the script information page.

// CHANGELOG
2.0.0 Correct algorithm to loop over *all* windows (DBK)
1.2  Format code/comments; put .txt in the report_Title instead of concat'ing
     to users chosen filename (DBK)
1.1  REVISED FILE NAMING METHOD
1.0  INITIAL RELEASE

// TERMS OF USE:
This work is licensed under the Creative Commons
Attribution-NonCommercial-ShareAlike 3.0 Unported License.

To view a copy of this license, visit
http://creativecommons.org/licenses/by-nc-sa/3.0/ or send a letter to Creative
Commons, 444 Castro Street, Suite 900, Mountain View, California, 94041, USA.


*)

-- user switches (you can change these!)

-- name of report title
property report_Title : "URL List from Safari Tabs.txt"

-- main program

-- prepare the list
set url_list to {}
set the date_stamp to ((the current date) as string)
set NoteTitle to report_Title & " | " & the date_stamp

-- get tabs from safari
tell application "Safari"
    activate
    set windowCount to number of windows
    repeat with win_n from 1 to windowCount
        set tabcount to number of tabs in window win_n
        repeat with tab_n from 1 to tabcount
            set TabTitle to name of tab tab_n of window win_n
            set TabURL to URL of tab tab_n of window win_n
            set TabInfo to ("" & TabTitle & return & TabURL & return & return)
            copy TabInfo to the end of url_list
        end repeat
    end repeat
end tell

-- convert url_list to text
set old_delim to AppleScript's text item delimiters
set AppleScript's text item delimiters to return
set url_list to (NoteTitle & return & return & return & url_list) as text
set AppleScript's text item delimiters to old_delim

-- choose file name for export
-- (wrapping in a finder function so the dialog works in alfred, km, etc.)
tell application "Finder"
    activate
    set save_File to choose file name with prompt "Name this file:" default name report_Title default location (path to desktop folder)
end tell

-- write the file
tell application "System Events"
    set save_File to open for access (save_File as string) with write permission
    try
        write url_list to save_File
    end try
    close access save_File
end tell
